extends PlayerState
class_name PlayerStateDash

@export var move_speed: float = 250.0
@export var effect_delay: float = 0.05
@export var dash_audio: AudioStream

var direction: Vector2 = Vector2.ZERO
var effect_timer: float = 0

func init() -> void:
	pass

func enter() -> void:
	player.invulnerable = true
	player.animation_player.play("dash")
	player.animation_player.animation_finished.connect(_on_animation_finished)
	direction = player.direction
	if direction == Vector2.ZERO:
		direction = player.axis_direction
	if dash_audio:
		player.audio_player.stream = dash_audio
		player.audio_player.play()
	effect_timer = 0

func exit() -> void:
	player.invulnerable = false
	player.animation_player.animation_finished.disconnect(_on_animation_finished)
	next_state = null

func handle_input(_event: InputEvent) -> PlayerState:
	return next_state 

func process(_delta: float) -> PlayerState:
	player.velocity = direction * move_speed
	effect_timer -= _delta
	if effect_timer < 0:
		effect_timer = effect_delay
		spawn_effect()
	return next_state

func physics_process(_delta: float) -> PlayerState:
	return next_state

func _on_animation_finished(_anim_name: String) -> void:
	next_state = idle

func spawn_effect() -> void:
	var effect: Node2D = Node2D.new()
	player.get_parent().add_child(effect)
	effect.global_position = player.global_position - Vector2(0, 0.1)
	effect.modulate = Color(1.5, 0.2, 1.25, 0.75)
	var sprite_copy: Sprite2D = player.sprite.duplicate()
	effect.add_child(sprite_copy)
	var tween: Tween = create_tween()
	tween.set_ease(Tween.EASE_OUT)
	tween.tween_property(effect, "modulate", Color(1, 1, 1, 0.0), 0.2)
	tween.chain().tween_callback(effect.queue_free)
